/*****************************************************************************
 *                                                                           *
 *                    Control file for the GAW9 dataset                      *
 *                                                                           *
 *****************************************************************************/

Array mark1(30),mark2(30),mark5(30),q(4),id(3)

DataDir = "../../../data/gaw9/" # Where the data files are located

/*
 * Phenotype data file 
 *
 * The format can be explained as follows:
 *  5x - skip 5 characters
 *  3(5) - read in 3 fields of 5 characters each
 *  15 - skip 15 characters
 *  2 - read in 1 field of 2 characters
 *  3 - read in 1 field of 3 characters
 *  6x - skip 6 characters
 *  6 - read in 1 field of 6 characters
 *  4(7) - read in 4 fields of 7 characters
 */
file [5x,3(5),15x,2,3,6x,6,4(7)] shell("zcat "+DataDir+"phenotyp.gz"),id,sx,age,ef,q

/* Genotype data file */
GS=""
file [5x,5,15(x,2)] "d1a1-15",id(1),(mark1(i),i=1,15)
file [5x,5,15(x,2)] "d1a16-30",id(1),(mark1(i),i=16,30)
file [5x,5,15(x,2)] shell("zcat "+DataDir+"d2a1-15"),id(1),(mark2(i),i=1,15)
file [5x,5,15(x,2)] shell("zcat "+DataDir+"d2a16-30"),id(1),(mark2(i),i=16,30)
file [5x,5,15(x,2)] shell("zcat "+DataDir+"d5a1-15"),id(1),(mark5(i),i=1,15)
file [5x,5,15(x,2)] shell("zcat "+DataDir+"d5a16-30"),id(1),(mark5(i),i=16,30)
	
/* Link haplotypes to the appropriate markers */
Marker Loci mark1,mark2,mark5

Super locus smark [mark1]

/* 
 * Only use genotype data where trait data available.  The gaw9 dataset has
 * marker data available on everyone (1497 individuals), but trait data only
 * on 1000 (non founder) individuals.  A more realistic analysis would be to
 * only use marker data on the 1000 individuals which have trait data.
 */
use mark1,mark2,mark5 where (q(1))

Trait Locus QTL     # Define QTL as 1 or more trait loci
pedigree id         # id(1), id(2), and id(3) defined as id, sire, dam
log "log"           # Name for log file
sex sx 1,2          # sx determines sex: 1=Male, 2=Female

/* Set up model */
model q(1)=age+ef+QTL

/* Define linkage group(s) */
Link "Chromosome 1",smark
#Link "Chromosome 2",mark2
#Link "Chromosome 5",mark5
